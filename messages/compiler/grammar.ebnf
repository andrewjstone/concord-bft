@@grammar::CMF

start = {msg:msg}+ $ ;

msg = 'Msg' name:name id:number '{' fields:{field}+ '}' ;

field = type:(compound | primitive) name:name ;
compound = kvpair:kvpair | list:list | map:map | oneof:oneof | optional:optional ;
kvpair = 'kvpair' key:primitive value:(primitive | compound) ;
list = 'list' type:(primitive | compound) ;
map = 'map' key:primitive value:(primitive | compound) ;
oneof = 'oneof' '{' names:{name}+ '}' ;
optional = 'optional' type:(primitive | compound) ;

primitive
  =
  | 'bool'
  | 'uint8'
  | 'uint16'
  | 'uint32'
  | 'uint64'
  | 'sint8'
  | 'sint16'
  | 'sint32'
  | 'sint64'
  | 'string'
  | 'bytes'
  ;

name = /[a-zA-Z_][a-zA-Z0-9_]*/ ;
number = /\d+/ ;

